#!/bin/csh -f
# $Id: 755ebfb1c458ef8a43d4b1225879c03dda202c27 $
#
# This file is a script to allow Scheme to be run out of a
# non-standard directory without recompiling the microcode.  After
# editing the following line to indicate where Scheme directories are,
# this script can be used to invoke Scheme.
set base="${HOME}/dist-7.0"

set program="$0"
set scheme="${base}/microcode/scheme"

set seen_utab="0"
set utab="${base}/lib/utabmd.bin"

set seen_band="0"
set band_option="-band"
set band="${base}/lib/runtime.bin"

set extra=()

while ($#argv != 0)

  switch ($argv[1])
    case "-fasl":
    case "-band":
      set band_option="$argv[1]"
      if ($#argv == 1) then
	echo "${program}: $band_option requires an argument"
	exit 1
      endif
      if ("$seen_band" != "0") then
	echo "${program}: Too many image specifiers"
	exit 1
      endif
      set seen_band="1"
      set band="$argv[2]"
      shift
    breaksw

    case "-utabmd":
    case "-utab":
      if ($#argv == 1) then
	echo "${program}: $argv[1] requires an argument"
	exit 1
      endif
      if ("$seen_utab" != "0") then
	echo "${program}: Too many utab specifiers"
	exit 1
      endif
      set seen_utab="1"
      set utab="$argv[2]"
      shift
    breaksw

    default:
      set extra=($extra $argv[1])
    breaksw
  endsw

  shift
end

# echo "$scheme" -utab "$utab" "$band_option" "$band" $extra
exec "$scheme" -utab "$utab" "$band_option" "$band" $extra
